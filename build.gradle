plugins {
    id 'application'
    id 'scala'
}

application {
    mainClass.set('ca.uwaterloo.flix.Main')
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.scala-lang:scala-library:2.13.15!!'
    implementation 'org.scala-lang:scala-reflect:2.13.15!!'

    implementation('org.java-websocket:Java-WebSocket:1.5.7')
    implementation('org.jline:jline:3.26.2')
    implementation('org.json4s:json4s-native_2.13:4.0.7')
    implementation('org.ow2.asm:asm:9.7')
    implementation('com.github.rjeschke:txtmark:0.13')
    implementation('com.github.scopt:scopt_2.13:4.1.0')
    implementation('com.google.guava:guava:33.3.1-jre')
    implementation 'org.apache.commons:commons-lang3:3.17.0'
    implementation("com.lihaoyi:sourcecode_2.13:0.4.2")

    implementation('io.get-coursier:coursier_2.13:2.1.14')

    // Note: The tomlj library determines the antlr library.
    // We cannot upgrade the antlr library independently.
    implementation('org.tomlj:tomlj:1.1.1')
    implementation('org.antlr:antlr4-runtime:4.11.1')

    // JLine uses a logger. We silence it here.
    implementation('org.slf4j:slf4j-nop:2.0.13')

    // Java implementation of LSP.
    implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j:0.23.1'

    testImplementation('org.scalatest:scalatest_2.13:3.2.19')
}

tasks.withType(ScalaCompile).configureEach {
    scalaCompileOptions.forkOptions.with {
        memoryMaximumSize = '1536m'
    }
    scalaCompileOptions.additionalParameters = [
            "-Xfatal-warnings",
            "-Ypatmat-exhaust-depth", "400",
            "-release", "11",
            "-opt:inline:ca.uwaterloo.**",
            "-Xmixin-force-forwarders:false",   // Required for LSP4j
            "-Xsource:3",                       // Scala 3 migration flag
            "-Ytasty-reader"                    // Scala 3 migration flag
    ]
    compileScala.sourceCompatibility = 21
    compileScala.targetCompatibility = 21
}

sourceSets {
    main {
        scala {
            srcDirs = ['main/src']
        }

        resources {
            srcDirs = ['main/src/resources']
        }
    }
    test {
        scala {
            srcDirs = ['main/test']
        }
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'ca.uwaterloo.flix.Main'
    }

    from {
        // This line has to come before the next
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
        configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    }

    from('main') {
        include '**/*.flix'
        include '**/*.json'
        include '**/*.zip'
        include '**/ClassList.txt'
    }

    duplicatesStrategy = 'exclude'
}

tasks.register('testAll', JavaExec) {
    dependsOn['testClasses']
    mainClass = 'org.scalatest.tools.Runner'
    // Note: We do not use the -P (parallelism) flag for two reasons:
    // (A) It may use excessive amounts of memory (running many instances of the compiler).
    // (B) It may garble the order of the output (when an error occurs).
    // Of course we still benefit from parallelism inside Flix itself.
    args = ['-s', 'ca.uwaterloo.flix.TestAll', '-o']
    // We also enforce that all tests can complete with 4GB of memory:
    jvmArgs = ['-Xmx3g']
    classpath = sourceSets.test.runtimeClasspath
    standardInput = System.in
}

task testFuzzerSuite(dependsOn: ['testClasses'], type: JavaExec) {
    mainClass = 'org.scalatest.tools.Runner'
    args = ['-s', 'flix.fuzzers.FuzzerSuite', '-o']
    classpath = sourceSets.test.runtimeClasspath
    standardInput = System.in
}

test.dependsOn testAll

task vscode(type: Copy, dependsOn: [clean, jar]) {
    from 'build/libs/flix.jar'
    into providers.gradleProperty('dev.flix.vscode.project')
}
