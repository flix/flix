plugins {
    id 'application'
    id 'scala'
}

application {
    mainClass.set('ca.uwaterloo.flix.Main')
}

repositories {
    mavenCentral()
}

dependencies {
    implementation('org.scala-lang:scala3-compiler_3:3.4.2')
    implementation('org.scala-lang:scala3-library_3:3.2.2')
    implementation 'org.scala-lang:scala-reflect:2.13.14!!'

    implementation('org.java-websocket:Java-WebSocket:1.5.6')
    implementation('org.jline:jline:3.26.3')
    implementation('org.json4s:json4s-native_2.13:4.0.7')
    implementation('org.ow2.asm:asm:9.7')
    implementation('com.github.rjeschke:txtmark:0.13')
    implementation('com.github.scopt:scopt_2.13:4.1.0')
    implementation('com.google.guava:guava:33.2.0-jre')

    // implementation("io.github.p-org.solvers:pjbdd:1.0.10-10-v5")
    implementation files('lib/pjbdd-v1.0.10-9-67-gf113b5a.jar')

    implementation('io.get-coursier:coursier_2.13:2.1.4')

    // Note: The tomlj library determines the antlr library.
    // We cannot upgrade the antlr library independently.
    implementation('org.tomlj:tomlj:1.1.1')
    implementation('org.antlr:antlr4-runtime:4.11.1')

    implementation('org.slf4j:slf4j-nop:2.0.13')

    testImplementation('org.scalatest:scalatest_3:3.2.19')
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.forkOptions.with {
        memoryMaximumSize = '1536m'
    }
    scalaCompileOptions.additionalParameters = [
            "-source:3.0-migration",
            "-Xignore-scala2-macros",
            "-Werror",
            "-release", "11",
            "-rewrite",
            "-no-indent"
    ]
    compileScala.sourceCompatibility = 21
    compileScala.targetCompatibility = 21
}

sourceSets {
    main {
        scala {
            srcDirs = ['main/src']
        }

        resources {
            srcDirs = ['main/src/resources']
        }
    }
    test {
        scala {
            srcDirs = ['main/test']
        }
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'ca.uwaterloo.flix.Main'
    }

    from {
        // This line has to come before the next
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
        configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    
    from('main') {
        include '**/*.flix'
        include '**/*.zip'
        include '**/ClassList.txt'
    }

    duplicatesStrategy = 'exclude'
}

task testAll(dependsOn: ['testClasses'], type: JavaExec) {
    mainClass = 'org.scalatest.tools.Runner'
    // Note: We do not use the -P (parallelism) flag for two reasons:
    // (A) It may use excessive amounts of memory (running many instances of the compiler).
    // (B) It may garble the order of the output (when an error occurs).
    // Of course we still benefit from parallelism inside Flix itself.
    args = ['-s', 'ca.uwaterloo.flix.TestAll', '-o']
    // We also enforce that all tests can complete with 4GB of memory:
    jvmArgs = ['-Xmx3g']
    classpath = sourceSets.test.runtimeClasspath
    standardInput = System.in
}

test.dependsOn testAll

task vscode(type: Copy, dependsOn: [clean, jar]) {
    from 'build/libs/flix.jar'
    into providers.gradleProperty('dev.flix.vscode.project')
}
