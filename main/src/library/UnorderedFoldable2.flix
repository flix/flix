/*
 *  Copyright 2022 Jakob Schneider Villumsen
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *  http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

///
/// A trait for unordered data structures that can be folded.
///
pub trait UnorderedFoldable2[t : Type -> Type] {
    type Aef: Eff

    ///
    /// Unordered fold of `t`.
    ///
    /// Applies `f` to all elements in `t` and combines the results
    /// i.e. `CommutativeMonoid.combine(f(a))` for all `a` in `t`.
    ///
    pub def foldMap(f: a -> b \ ef, t: t[a]): b \ UnorderedFoldable2.Aef[t] + ef with CommutativeMonoid[b]

    ///
    /// Returns the result of applying `CommutativeMonoid.combine` to all the elements in `t`.
    ///
    pub def fold(t: t[a]): a \ UnorderedFoldable2.Aef[t] with CommutativeMonoid[a] =
        UnorderedFoldable2.foldMap(identity, t)

    ///
    /// Returns true if and only if `t` is empty.
    ///
    pub def isEmpty(t: t[a]): Bool \ UnorderedFoldable2.Aef[t] =
        UnorderedFoldable2.forAll(_ -> false, t)

    ///
    /// Returns `true` if and only if at least one element in `t` satisfies the predicate `f`.
    ///
    /// Returns `false` if `t` is empty.
    ///
    pub def exists(f: a -> Bool \ ef, t: t[a]): Bool \ UnorderedFoldable2.Aef[t] + ef =
        use SemiGroup.Any.Any;
        let Any(x) = UnorderedFoldable2.foldMap(a -> Any(f(a)), t);
        x

    ///
    /// Returns `true` if and only if all elements in `t` satisfy the predicate `f`.
    ///
    /// Returns `true` if `t` is empty.
    ///
    pub def forAll(f: a -> Bool \ ef, t: t[a]): Bool \ UnorderedFoldable2.Aef[t] + ef =
        use SemiGroup.All.All;
        let All(x) = UnorderedFoldable2.foldMap(a -> All(f(a)), t);
        x

    ///
    /// Returns `true` if and only if the element `x` is in `t`.
    ///
    pub def memberOf(x: a, t: t[a]): Bool \ UnorderedFoldable2.Aef[t] with Eq[a] =
        UnorderedFoldable2.exists(y -> x == y, t)

    ///
    /// Returns the number of elements in `t`.
    ///
    pub def size(t: t[a]): Int32 \ UnorderedFoldable2.Aef[t] =
        UnorderedFoldable2.foldMap(_ -> 1, t)

    ///
    /// Returns the number of elements in `t` that satisfy the predicate `f`.
    ///
    pub def count(f: a -> Bool \ ef, t: t[a]): Int32 \ UnorderedFoldable2.Aef[t] + ef =
        UnorderedFoldable2.foldMap(a -> if (f(a)) 1 else 0, t)

}
